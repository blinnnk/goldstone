apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-platform-android'
apply plugin: 'kotlin-kapt'

android {
  packagingOptions {
    exclude 'META-INF/main.kotlin_module'
  }

  signingConfigs {
    release {
      storeFile file('/Users/saithkay/goldstoneAndroidKeystore')
      storePassword signingKeystorePassword
      keyAlias signingKeystoreAlias
      keyPassword signingKeystorePassword
    }
  }
  compileSdkVersion 28
  defaultConfig {
    applicationId "io.goldstone.blinnnk"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 24
    versionName "1.7.2"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    kotlinOptions.allWarningsAsErrors = true
    renderscriptSupportModeEnabled true
    vectorDrawables.useSupportLibrary = true
    manifestPlaceholders = [
      XG_ACCESS_ID : "2100282353",
      XG_ACCESS_KEY: xinGePushAccessKey
    ]
    ndk {
      abiFilters "armeabi", "armeabi-v7a", "x86" // PC 调试的时候恢复 `x86`
    }
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    // Leave only the supported languages
    resConfigs "en", "ja", "ru", "zh_CN", "zh_HK", "zh_TW", "ko"
  }

  // 自定义输出apk名字
  applicationVariants.all {
    variant ->
      variant.outputs.all { output ->
        if (buildType.name == 'release') {
          outputFileName = new File("app_v" + defaultConfig.versionName + "_" +
            new Date().format("yyyy-MM-dd-HH-mm-ss") + "_" +
            buildType.name + ".apk")
        }
      }
  }

  buildTypes {
    debug {
      minifyEnabled false
      zipAlignEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      zipAlignEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  kapt {
    generateStubs = true
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  //noinspection GradleCompatible
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support.constraint:constraint-layout:1.1.3'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test:rules:1.0.2'
  // Glide Image Loader
  implementation 'com.github.bumptech.glide:glide:4.8.0'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
  // Recycler View
  implementation 'com.android.support:cardview-v7:28.0.0'
  implementation 'com.android.support:recyclerview-v7:28.0.0'
  // Kotlin
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  // ANKO
  implementation 'org.jetbrains.anko:anko:0.10.8'
  implementation 'com.afollestad.material-dialogs:core:2.0.0-alpha12'
  // HoneyUI By Blink Co.
  implementation 'com.blinnnk:honeyui:0.3.5.3'
  // Crypto Ethereum
  implementation 'org.ethereum:geth:1.8.11'
  // Crypto Bitcoin
  implementation 'cash.bitcoinj:bitcoinj-core:0.14.5.2'
  // Support PBKDF2withHmacSHA512 Below Api 26
  implementation 'com.madgag.spongycastle:core:1.58.0.0'
  implementation 'com.madgag.spongycastle:prov:1.58.0.0'
  // Crypt slf4j
  implementation 'org.slf4j:slf4j-api:1.8.0-beta2'
  // Room Database
  implementation 'android.arch.persistence.room:runtime:1.1.1'
  kapt 'android.arch.persistence.room:compiler:1.1.1'
  // OkHttp
  implementation 'com.squareup.okhttp3:okhttp:3.12.0'
  implementation 'com.google.code.gson:gson:2.8.5'
  // Event Bus
  implementation 'org.greenrobot:eventbus:3.1.1'
  // ZXing QR Code
  implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
  implementation 'com.google.zxing:core:3.3.3'
  // XinGe Push
  implementation 'com.tencent.xinge:xinge:3.2.7-Release'
  implementation 'com.tencent.jg:jg:1.1'
  implementation 'com.tencent.wup:wup:1.0.0.E-release'
  implementation 'com.tencent.mid:mid:4.0.6-release'
  // Google Analytics
  implementation 'com.google.android.gms:play-services-analytics:16.0.6'
  // Wave Animation
  implementation 'me.itangqi.waveloadingview:library:0.3.5'
  // MPAndroidChart
  implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
}

apply plugin: 'com.google.gms.google-services'
repositories {
  mavenCentral()
}